
%BigHud(loc, text, color)
    Hud
        Visible_To: Event Player
        Header: text
        Subheader: Null
        Text: Null
        Location: loc
        Sort_Order: 0
        Header_Color: color
        Subheader_Color: White
        Text_Color: White
        Reevaluation: Visible To And String

%SmallHud(loc, text, color)
    Hud
        Visible_To: Event Player
        Header: Null
        Subheader: Null
        Text: text
        Location: loc
        Sort_Order: 999
        Header_Color: White
        Subheader_Color: White
        Text_Color: color
        Reevaluation: Visible To And String

%PlayEffect(positionv, type, effectcolor)
    Play Effect
        Visible_To: Event Player
        Type: type
        Color: effectcolor
        Position: positionv
        Radius: 1.5

%CreateEffect(pos, type, color)
    Create Effect
        Visible_To: Everyone
        Type: type
        Color: color
        Position: pos
        Radius: 1.5
        Reevaluation: Visible To

%GameText(who, text, pos, scale)
    World Text
        Visible_To: who
        Header: text
        Position: pos
        Scale: scale
        Clipping: Clip Against Surfaces
        Reevaluation: Visible To And String

%ResetPlayerUI(player_)
    BigHud(Top, `Money: {}`(pvar money), White)
    BigHud(Left, `Attack Fast: {}%`(pvar projectileBoost), White)
    BigHud(Left, `Run: {}%`(pvar speedBoost), White)
    BigHud(Left, `Damage: {}%`(pvar damageBoost), White)
    BigHud(Left, `Max Life: {}%`(pvar healthBoost), White)
    BigHud(Left, `Healing Over Time: {}`(pvar healthTime), White)
    BigHud(Left, `Money On Kill: {}`(pvar moneyElim), White)
    BigHud(Left, `Money Over Time: {}`(pvar moneyTime), White)

    // Create game texts per player.
    GameText(Event Player, pvar gmoneyTime[3], (pvar gmoneyTime[0] + Up), 1.5)
    GameText(Event Player, pvar gmoneyElim[3], (pvar gmoneyElim[0] + Up), 1.5)
    GameText(Event Player, pvar gdamageBoost[3], (pvar gdamageBoost[0] + Up), 1.5)
    GameText(Event Player, pvar gprojectileBoost[3], (pvar gprojectileBoost[0] + Up), 1.5)

    GameText(Event Player, pvar ghealthBoost[3], (pvar ghealthBoost[0] + Up), 1.5)
    GameText(Event Player, pvar ghealthTime[3], (pvar ghealthTime[0] + Up), 1.5)
    GameText(Event Player, pvar gspeedBoost[3], (pvar gspeedBoost[0] + Up), 1.5)

%CreateBuyCircle(location, color, text)
   CreateEffect(location, ring, color)

%CreateBuyCirclePlayer(location, color, text)
    Create Effect
        Visible_To: Event Player
        Type: ring
        Color: color
        Position: location
        Radius: 1.5
        Reevaluation: Visible To
    //GameText(Everyone, text, (location + Up), 1.5)

Rule "Upgrade Shop By Zencep#1489. Join us at discord.gg/5GhnRJy to keep up to date."
    Event
        On Global

Rule "More information on the Overwatch forum post: bit.ly/FtoUpgradePost"
    Event
        On Global

Rule "Setup Global"
    Event
        On Global
    Actions
        if get_map() ==  Havana:
            gvar shopTeleportLocation = <-15, 6, -80>
            gvar exitPortalLocation = <-4.5, 6, -79.25>
            gvar buyMultiplierLocation = <-2, 6, -92>
            gvar gcommitSuicideLocation = <-8, 6, -92>
            gvar gprestigeLocation = <-14.04, 6, -89.95>

            gvar gmoneyTimeLocation = <-18, 5.85, -77.25>
            gvar gmoneyElimLocation = <-22, 5.85, -77.25>
            gvar gdamageBoostLocation = <-26, 5.85, -77.25>
            gvar gprojectileBoostLocation = <-30, 5.85, -77.25>

            gvar ghealthBoostLocation = <-18, 5.85, -81.25>
            gvar ghealthTimeLocation = <-22, 5.85, -81.25>
            gvar gspeedBoostLocation = <-26, 5.85, -81.25>

            gvar gunlockAbilityOneLocation = <-16, 6.03, -66.5>
            gvar gunlockUltimateLocation = <-20, 6.03, -66.5>
            gvar gunlockAbilityTwoLocation = <-24, 6.03, -66.5>

            gvar gunlockLowGravityLocation = <-8, 6.03, -66.5>
            gvar gunlockAimbotLocation = <0, 6.03, -66.5>
        elif get_map() == Hollywood:
            gvar shopTeleportLocation = <-13, 1.6, 5>
            gvar exitPortalLocation = <-8.5, 1.6, -4>
            gvar buyMultiplierLocation = <-26, 1.75, -14>
            gvar gcommitSuicideLocation = <-23.5, 7.75, 10.5>
            gvar gprestigeLocation = <-35.10, 1.75, -6.69>

            gvar gmoneyTimeLocation = <-20, 1.6, 6>
            gvar gmoneyElimLocation = <-24, 1.6, 3.6>
            gvar gdamageBoostLocation = <-28, 1.6, 1.2>
            gvar gprojectileBoostLocation = <-32, 1.6, -1.2>

            gvar ghealthBoostLocation = <-16.2, 1.6, -1>
            gvar ghealthTimeLocation = <-20.2, 1.6, -3.5>
            gvar gspeedBoostLocation = <-24.2, 1.6, -6>

            GameText(Everyone, `Abilities Here`, <-20.36, 7, -7.22>, 1.5)
            gvar gunlockAbilityOneLocation = <-14.3, 1.75, -15>
            gvar gunlockUltimateLocation = <-16.35, 1.75, -11.2>
            gvar gunlockAbilityTwoLocation = <-18.4, 1.6, -8.1>

            gvar gunlockLowGravityLocation = <-20.8, 5.75, -14.75>
            gvar gunlockAimbotLocation = <-17.3, 5.75, -12.5>
        elif get_map() == Eichenwalde:
            gvar shopTeleportLocation = <114.52, 10.72, -23.6>
            gvar exitPortalLocation = <112.71, 8.72, -31.96>
            gvar buyMultiplierLocation = <129.23, 8.72, -38.12>
            gvar gcommitSuicideLocation = <130.22, 14.74, -36.14>
            gvar gprestigeLocation = <120.22, 10.72, -14.68>

            gvar gmoneyTimeLocation = <121, 8.72, -27.5>
            gvar gmoneyElimLocation = <120, 8.72, -31.5>
            gvar gdamageBoostLocation = <119, 8.72, -35.5>
            gvar gprojectileBoostLocation = <118, 8.72, -39.5>

            gvar ghealthBoostLocation = <126, 8.72, -28.75>
            gvar ghealthTimeLocation = <125, 8.72, -32.75>
            gvar gspeedBoostLocation = <124, 8.72, -36.75>

            GameText(Everyone, `Abilities Here`, <115.12, 13, -13.67>, 1.5)
            gvar gunlockAbilityOneLocation = <132, 10.72, 7.25>
            gvar gunlockUltimateLocation = <128, 10.72, 8.5>
            gvar gunlockAbilityTwoLocation = <124, 10.72, 9.75>

            gvar gunlockLowGravityLocation = <137, 10.72, 0>
            gvar gunlockAimbotLocation = <138.25, 10.72, 4>

        /*
            Zerod Locations for new maps..

            gvar shopTeleportLocation = <0, 0, 0>
            gvar exitPortalLocation = <0, 0, 0>
            gvar buyMultiplierLocation = <0, 0, 0>
            gvar gcommitSuicideLocation = <0, 0, 0>

            gvar gmoneyTimeLocation = <0, 0, 0>
            gvar gmoneyElimLocation = <0, 0, 0>
            gvar gdamageBoostLocation = <0, 0, 0>
            gvar gprojectileBoostLocation = <0, 0, 0>

            gvar ghealthBoostLocation = <0, 0, 0>
            gvar ghealthTimeLocation = <0, 0, 0>
            gvar gspeedBoostLocation = <0, 0, 0>

            gvar gunlockAbilityOneLocation = <0, 0, 0>
            gvar gunlockUltimateLocation = <0, 0, 0>
            gvar gunlockAbilityTwoLocation = <0, 0, 0>

            gvar gunlockLowGravityLocation = <0, 0, 0>
            gvar gunlockAimbotLocation = <0, 0, 0>
         */

        gvar matchTimeSeconds = 0
        gvar matchTimeMultiplier = 1
        gvar gameStopping = false

        gvar gunlockAbilityOne = [gunlockAbilityOneLocation, Purple, 350, `Unlock Ability 1: {}`(350)]
        gvar gunlockUltimate = [gunlockUltimateLocation, Purple, 700, `Unlock Ultimate Ability: {}`(700)]
        gvar gunlockAbilityTwo = [gunlockAbilityTwoLocation, Purple, 350, `Unlock Ability 2: {}`(350)]

        gvar gcommitSuicide = [gcommitSuicideLocation, Red, 500, `Die Here: {}`(500)]
        gvar gunlockLowGravity = [gunlockLowGravityLocation, Yellow, 1500000, `Slow Fall: {}`(1500000), 1]
        gvar gunlockAimbot = [gunlockAimbotLocation, Red, 10000000, `Banned Attack: {}`(10000000), 1]
        /*
         [
            0 => location
            1 => color
            2 => price
            3 => text
            4 => prestige/level minimum
         ]
         */

        // index = prestige
        gvar gprestigeCosts = [0, 50000, 125000, 250000]
        gvar gprestigeMax = 3 // 4 total not including prestige 0

        /*
            Prestige Caps

            [
                index/level => cap number
            ]
        */
        gvar gmoneyTimeCaps = [900, 2250, 3500, 5000]
        gvar gmoneyElimCaps = [1250, 2500, 4500, 7500]
        gvar gdamageBoostCaps = [1000, 2500, 5000, 10000]
        gvar gprojectileBoostCaps = [45, 65, 85, 100]
        gvar ghealthBoostCaps = [4500, 7500, 10000, 20000]
        gvar ghealthTimeCaps = [900, 2500, 5000, 15000]

Rule "Setup Store Locations"
    Event
        On Global
    Actions
        CreateBuyCircle(gunlockAbilityOne[0], gunlockAbilityOne[1], gunlockAbilityOne[3])
        GameText(Everyone, gunlockAbilityOne[3], (gunlockAbilityOne[0] + Up), 1.5)
        CreateBuyCircle(gunlockUltimate[0], gunlockUltimate[1], gunlockUltimate[3])
        GameText(Everyone, gunlockUltimate[3], (gunlockUltimate[0] + Up), 1.5)
        CreateBuyCircle(gunlockAbilityTwo[0], gunlockAbilityTwo[1], gunlockAbilityTwo[3])
        GameText(Everyone, gunlockAbilityTwo[3], (gunlockAbilityTwo[0] + Up), 1.5)


        CreateBuyCircle(gcommitSuicide[0], gcommitSuicide[1], gcommitSuicide[3])
        GameText(Everyone, gcommitSuicide[3], (gcommitSuicide[0] + Up), 1.5)

        CreateBuyCircle(gunlockLowGravity[0], gunlockLowGravity[1], gunlockLowGravity[3])
        GameText(Everyone, gunlockLowGravity[3], (gunlockLowGravity[0] + Up), 1.5)
        GameText(Everyone, `Unlock Level: {}`(gunlockLowGravity[4]), (gunlockLowGravity[0] + <0, 1.5, 0>), 1.5)
        CreateBuyCircle(gunlockAimbot[0], gunlockAimbot[1], gunlockAimbot[3])
        GameText(Everyone, gunlockAimbot[3], (gunlockAimbot[0] + Up), 1.5)
        GameText(Everyone, `Unlock Level: {}`(gunlockAimbot[4]), (gunlockAimbot[0] + <0, 1.5, 0>), 1.5)

        CreateEffect(exitPortalLocation, light shaft, Red)
        GameText(Everyone, `Exit`, (exitPortalLocation + Up), 1.5)

        CreateEffect(buyMultiplierLocation, ring, Blue)
        GameText(Everyone, `Buy More On Upgrade`, (buyMultiplierLocation + Up), 1.5)

Rule "Setup Player"
    Event
        On Each Player
        All
        All
    Actions
        Wait(0.016, Ignore Condition)

        Set Ability 1 Enabled
            Event Player
            false
        Set Ability 2 Enabled
            Event Player
            false
        Set Ultimate Ability Enabled
            Event Player
            false

        pvar money = (10 * gvar matchTimeMultiplier) * gvar matchTimeSeconds
        pvar moneyTime = 10
        pvar moneyElim = 15
        pvar healthBoost = 0
        pvar healthTime = 0
        pvar damageBoost = 0
        pvar speedBoost = 0
        pvar projectileBoost = 0
        pvar unlockedAbilityOne = false
        pvar unlockedAbilityTwo = false
        pvar unlockedUltimate = false
        pvar unlockedLowGravity = false
        pvar unlockedAimbot = false
        pvar shopOnCooldown = false
        pvar shopCooldownTimer = 0
        pvar shopCooldownTimerTextId = 0
        pvar playerInShop = false
        pvar playerShopConnectingTimer = 0
        pvar holdingTeleportKey = false
        pvar takenDamage = false
        pvar isMoving = false
        pvar buyMultiplier = 1
        pvar lowGravityEnabled = false
        pvar aimbotEnabled = false
        pvar currentPrestige = 0
        pvar nextPrestige = 1
        pvar nextPrestigeCost = 50000

        pvar gmoneyTime = [gmoneyTimeLocation, White, 100, `{}: {}`(`{} {}`("Upgrade", `{} {}`("Money", "Time")), 100 * pvar buyMultiplier)]
        pvar gmoneyElim = [gmoneyElimLocation, White, 100,  `{}: {}`(`{} {}`("Upgrade", `{} {} {}`("Money", "On", "Kill")), 100 * pvar buyMultiplier)]
        pvar gdamageBoost = [gdamageBoostLocation, Red, 70, `Upgrade Damage: {}`(70 * pvar buyMultiplier)]
        pvar gprojectileBoost = [gprojectileBoostLocation, Blue, 50, `Upgrade Attack Fast: {}`(50 * pvar buyMultiplier)]

        pvar ghealthBoost = [ghealthBoostLocation, White, 50, `Upgrade Life: {}`(50 * pvar buyMultiplier)]
        pvar ghealthTime = [ghealthTimeLocation, White, 150, `Upgrade Healing: {}`(150 * pvar buyMultiplier)]
        pvar gspeedBoost = [gspeedBoostLocation, Green, 750, `Upgrade Run: {}`(750 * pvar buyMultiplier)]

        CreateBuyCircle(pvar gmoneyTime[0], pvar gmoneyTime[1], pvar gmoneyTime[3])
        CreateBuyCircle(pvar gmoneyElim[0], pvar gmoneyElim[1], pvar gmoneyElim[3])
        CreateBuyCircle(pvar gdamageBoost[0], pvar gdamageBoost[1], pvar gdamageBoost[3])
        CreateBuyCircle(pvar gprojectileBoost[0], pvar gprojectileBoost[1], pvar gprojectileBoost[3])

        CreateBuyCircle(pvar ghealthBoost[0], pvar ghealthBoost[1], pvar ghealthBoost[3])
        CreateBuyCircle(pvar ghealthTime[0], pvar ghealthTime[1], pvar ghealthTime[3])
        CreateBuyCircle(pvar gspeedBoost[0], pvar gspeedBoost[1], pvar gspeedBoost[3])

        CreateBuyCircle(gprestigeLocation, Purple, `Buy Level`)
        // gprestigeCosts[pvar nextPrestige]
        GameText(Event Player, `Buy Next Level: {}`(pvar nextPrestigeCost), (gprestigeLocation + Up), 1.5)

        ResetPlayerUI(Event Player)

// Shop Methods and Loops
Rule "Enter Shop" // Modify this will teleport code after upgrades
    Event
        On Each Player
        All
        All
    Conditions
        Event Player.interacting
        pvar shopOnCooldown == false
        pvar playerInShop == false
        pvar playerShopConnectingTimer >= 3
    Actions
        Set Status
            Event Player
            Null
            Invincible
            9999
        Set Status
            Event Player
            Null
            Phased Out
            9999
        Set Damage Dealt
            Event Player
            0
        Wait(0.25, Ignore Condition)
        Teleport
            Event Player
            shopTeleportLocation
        pvar playerInShop = true
        pvar playerShopConnectingTimer = 0
        pvar holdingTeleportKey = false

%ExitLogic
    Clear Status
        Event Player
        Invincible
    Clear Status
        Event Player
        Phased Out
    Set Damage Dealt
        Event Player
        100 + pvar damageBoost
    pvar shopOnCooldown = true
    pvar playerInShop = false
    BigHud(Left, `Cooldown: {}`(16 - pvar shopCooldownTimer), Red)
    pvar shopCooldownTimerTextId = Last Text Id

Rule "Exit Shop"
    Event
        On Each Player
        All
        All
    Conditions
        Distance Between
            Event Player
            gvar exitPortalLocation
        <= 1.5
    Actions
        ExitLogic()
        Respawn(Event Player)

Rule "Shop Loop - Cooldown Timer"
    Event
        On Each Player
        All
        All
    Conditions
        pvar shopOnCooldown
    Actions
        pvar shopCooldownTimer += 1
        Wait(1s, Ignore Condition)
        Loop If Condition Is True

Rule "Shop - Reset Cooldown State"
    Event
        On Each Player
        All
        All
    Conditions
        pvar shopCooldownTimer > 15
    Actions
        pvar shopOnCooldown = false
        pvar shopCooldownTimer = 0
        Destroy HUD Text
            pvar shopCooldownTimerTextId


Rule "Shop Loop - Hold Interact Counter"
    Event
        On Each Player
        All
        All
    Conditions
        Event Player.interacting
        Event Player.moving == false
        pvar shopOnCooldown == false
        pvar playerInShop == false
        Is Alive(Event Player)
    Actions
        Msg(Event Player, `Trade Connecting: {}`(3 - pvar playerShopConnectingTimer))
        pvar holdingTeleportKey = true
        Wait(1s, Ignore Condition)
        pvar playerShopConnectingTimer += 1
        Loop If Condition Is True

Rule "Shop - Interact No Longer Held"
    Event
        On Each Player
        All
        All
    Conditions
        pvar holdingTeleportKey
        Event Player.interacting == false
    Actions
        pvar playerShopConnectingTimer = 0
        pvar holdingTeleportKey = false

Rule "Shop Loop - Holding Interact In Combat"
    Event
        Player Took Damage
        All
        All
    Conditions
        pvar holdingTeleportKey
        pvar takenDamage
    Actions
        pvar playerShopConnectingTimer = 0
        pvar holdingTeleportKey = false
        Msg(Event Player, `Trade Connect Stopped`)


// Looping Events
Rule "Loop (Global) - Autorestart"
    Event
        On Global
    Conditions
        Is Game In Progress
        gvar gameStopping != true
        gvar matchTimeSeconds > 2500
    Actions
        if gvar matchTimeSeconds == 2700:
            Big Msg(All Players(All Teams), `{} {} M`("Next Game", 5))
        elif gvar matchTimeSeconds == 2880:
            Big Msg(All Players(All Teams), `{} {} M`("Next Game", 2))
        elif gvar matchTimeSeconds >= 3000:
            gvar gameStopping = true
            Set Match Time
                5
            Enable Built-In Game Mode Scoring
            Declare Match Draw

        Wait(0.016s, Ignore Condition)
        Loop If Condition Is True

Rule "Loop - Damage Boost"
    Event
        On Each Player
        All
        All
    Conditions
        pvar damageBoost > 0
    Actions
        Set Damage Dealt
            Event Player
            100 + pvar damageBoost

Rule "Loop - Money Generator"
    Event
        On Each Player
        All
        All
    Conditions
        pvar playerInShop != true
    Actions
        pvar money += pvar moneyTime
        Wait(3, Ignore Condition)
        Loop

Rule "Loop - Money On Player Kill"
    Event
        Player Earned Elimination
        All
        All
    Conditions
        Attacker != Victim
    Actions
        pvar money += pvar moneyElim

Rule "Loop - Money On Player Final Hit"
    Event
        Player Dealt Final Blow
        All
        All
    Conditions
        Attacker != Victim
    Actions
        pvar money += (50 + pvar moneyElim)

Rule "Loop - Money on Damage Dealt"
    Event
        Player Dealt Damage
        All
        All
    Conditions
        Attacker != Victim
    Actions
        pvar money += (Event Damage * 0.25);

Rule "Loop - Late Joins Money Generatior"
    Event
        On Global
    Conditions
        Is Game In Progress
    Actions
        gvar matchTimeSeconds += 1

        if gvar matchTimeSeconds % 120 == 0:
            gvar matchTimeMultiplier += 1

        Wait(1s, Ignore Condition)
        Loop If Condition Is True

Rule "Loop - Disable Global Variables"
    Event
        On Global
    Conditions
        Is Game In Progress
        gvar gameStopping != true
    Actions
        Set Match Time
            10000
        Disable Built-In Game Mode Scoring

Rule "Loop - Disable Player Variables"
    Event
        On Each Player
        All
        All
    Conditions
        Is Game In Progress
    Actions
        Modify Player Score
            Event Player
            0
        Set Match Time
            3600

Rule "Loop - Check Player In Combat (Took)"
    Event
        Player Took Damage
        All
        All
    Actions
        pvar takenDamage = true

Rule "Loop - Check Player In Combat (Dealt)"
    Event
        Player Dealt Damage
        All
        All
    Actions
        pvar takenDamage = true

Rule "Loop - Check Player Out Of Combat"
    Event
        On Each Player
        All
        All
    Conditions
        pvar takenDamage
    Actions
        Wait(1s, Ignore Condition)
        pvar takenDamage = false

Rule "Loop - Check Player Is Moving"
    Event
        On Each Player
        All
        All
    Conditions
        Event Player.moving
    Actions
        pvar isMoving = true

Rule "Loop - Check Player Is Not Moving"
    Event
        On Each Player
        All
        All
    Conditions
        Event Player.moving == false
    Actions
        pvar isMoving = false


%GenBuyEvent
    Event
        On Each Player
        All
        All

%GenBuyConditions(price, pos)
    Event Player.interacting == true
    Distance Between
        Event Player
        pos
    <= 1.25
    pvar money >= price
    // Maybe add check for playerInShop?

%GenBuyBaseActionsPre(price, pos, color)
    pvar money -= price
    Msg(Event Player, `{} {}`("Upgrade", "Bought"))
    Play Effect
        Visible_To: Everyone
        Type: ring explosion
        Color: color
        Position: pos
        Radius: 3

%GenBuyBaseActionsPost
    Skip If(Event Player.crouching != true, 2)
    Wait(0.016, Ignore Condition)
    Loop If Condition Is True

// Buy Rules
Rule "Buy Money / Time"
    GenBuyEvent()
    Conditions
        GenBuyConditions((pvar gmoneyTime[2] * pvar buyMultiplier), pvar gmoneyTime[0])
        pvar moneyTime < gmoneyTimeCaps[pvar currentPrestige]
    Actions
        GenBuyBaseActionsPre((pvar gmoneyTime[2] * pvar buyMultiplier), pvar gmoneyTime[0], pvar gmoneyTime[1])
        pvar moneyTime += (2 * pvar buyMultiplier)
        GenBuyBaseActionsPost()

Rule "Buy Money On Kill"
    GenBuyEvent()
    Conditions
        GenBuyConditions((pvar gmoneyElim[2] * pvar buyMultiplier), pvar gmoneyElim[0])
        pvar moneyElim < gmoneyElimCaps[pvar currentPrestige]
    Actions
        GenBuyBaseActionsPre((pvar gmoneyElim[2] * pvar buyMultiplier), pvar gmoneyElim[0], pvar gmoneyElim[1])
        pvar moneyElim += (15 * pvar buyMultiplier)
        GenBuyBaseActionsPost()

Rule "Buy Health Boost"
    GenBuyEvent()
    Conditions
        GenBuyConditions((pvar ghealthBoost[2] * pvar buyMultiplier), pvar ghealthBoost[0])
        pvar healthBoost < ghealthBoostCaps[pvar currentPrestige]
    Actions
        GenBuyBaseActionsPre((pvar ghealthBoost[2] * pvar buyMultiplier), pvar ghealthBoost[0], pvar ghealthBoost[1])
        pvar healthBoost += (2 * pvar buyMultiplier)
        Set Max Health
            Event Player
            100 + pvar healthBoost
        Heal
            Event Player
            Null
            Max Health(Event Player)
        GenBuyBaseActionsPost()

Rule "Buy Health Regen"
    GenBuyEvent()
    Conditions
        GenBuyConditions((pvar ghealthTime[2] * pvar buyMultiplier), pvar ghealthTime[0])
        pvar healthTime < ghealthTimeCaps[pvar currentPrestige]
    Actions
        GenBuyBaseActionsPre((pvar ghealthTime[2] * pvar buyMultiplier), pvar ghealthTime[0], pvar ghealthTime[1])

        pvar healthTime += (2 * pvar buyMultiplier)

        Stop Heal Over Time
            Last Heal Over Time ID

        Start Heal Over Time
            Event Player
            Event Player
            9999
            pvar healthTime

        GenBuyBaseActionsPost()

Rule "Buy Damage Boost"
    GenBuyEvent()
    Conditions
        GenBuyConditions((pvar gdamageBoost[2] * pvar buyMultiplier), pvar gdamageBoost[0])
        pvar damageBoost < gdamageBoostCaps[pvar currentPrestige]
    Actions
        GenBuyBaseActionsPre((pvar gdamageBoost[2] * pvar buyMultiplier), pvar gdamageBoost[0], pvar gdamageBoost[1])
        pvar damageBoost += (2 * pvar buyMultiplier)
        GenBuyBaseActionsPost()

Rule "Buy Speed Boost"
    GenBuyEvent()
    Conditions
        GenBuyConditions((pvar gspeedBoost[2] * pvar buyMultiplier), pvar gspeedBoost[0])
        pvar speedBoost < 100
    Actions
        GenBuyBaseActionsPre((pvar gspeedBoost[2] * pvar buyMultiplier), pvar gspeedBoost[0], pvar gspeedBoost[1])
        pvar speedBoost += (1 * pvar buyMultiplier)
        Set Move Speed
            Event Player
            100 + pvar speedBoost
        GenBuyBaseActionsPost()

Rule "Buy Projectile Speed"
    GenBuyEvent()
    Conditions
        GenBuyConditions((pvar gprojectileBoost[2] * pvar buyMultiplier), pvar gprojectileBoost[0])
        pvar projectileBoost < gprojectileBoostCaps[pvar currentPrestige]
    Actions
        GenBuyBaseActionsPre((pvar gprojectileBoost[2] * pvar buyMultiplier), pvar gprojectileBoost[0], pvar gprojectileBoost[1])
        pvar projectileBoost += (2 * pvar buyMultiplier)
        Set Projectile Speed
            Event Player
            100 + pvar projectileBoost
        GenBuyBaseActionsPost()

Rule "Buy Next Prestige / Level"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gprestigeCosts[pvar nextPrestige], gprestigeLocation)
        pvar currentPrestige < gprestigeMax
    Actions
        GenBuyBaseActionsPre(gprestigeCosts[pvar nextPrestige], gprestigeLocation, Purple)
        pvar currentPrestige += 1
        pvar nextPrestige += 1
        pvar nextPrestigeCost = gprestigeCosts[pvar nextPrestige]

        pvar money = (10 * gvar matchTimeMultiplier) * gvar matchTimeSeconds
        pvar moneyTime = 10
        pvar moneyElim = 15
        pvar healthBoost = 0
        pvar healthTime = 0
        pvar damageBoost = 0
        pvar speedBoost = 0
        pvar projectileBoost = 0
        pvar unlockedAbilityOne = false
        pvar unlockedAbilityTwo = false
        pvar unlockedUltimate = false
        pvar unlockedLowGravity = false
        pvar unlockedAimbot = false
        pvar lowGravityEnabled = false
        pvar aimbotEnabled = false

        Set Max Health
            Event Player
            100 + pvar healthBoost
        Heal
            Event Player
            Null
            Max Health(Event Player)

        Stop Heal Over Time
            Last Heal Over Time ID

        Start Heal Over Time
            Event Player
            Event Player
            9999
            pvar healthTime

        Set Move Speed
            Event Player
            100 + pvar speedBoost

        Set Projectile Speed
            Event Player
            100 + pvar projectileBoost

        Set Ability 1 Enabled
            Event Player
            False

        Set Ability 2 Enabled
            Event Player
            False

        Set Ultimate Ability Enabled
            Event Player
            False

        Set Gravity
            Event Player
            100

        Modify Player Score
            Event Player
            1

Rule "Buy Ability One"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gunlockAbilityOne[2], gunlockAbilityOne[0])
        pvar unlockedAbilityOne != true
    Actions
        GenBuyBaseActionsPre(gunlockAbilityOne[2], gunlockAbilityOne[0], gunlockAbilityOne[1])
        pvar unlockedAbilityOne = true
        Set Ability 1 Enabled
            Event Player
            True
        GenBuyBaseActionsPost()

Rule "Buy Ability Two"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gunlockAbilityTwo[2], gunlockAbilityTwo[0])
        pvar unlockedAbilityTwo != true
    Actions
        GenBuyBaseActionsPre(gunlockAbilityTwo[2], gunlockAbilityTwo[0], gunlockAbilityTwo[1])
        pvar unlockedAbilityTwo = true
        Set Ability 2 Enabled
            Event Player
            True
        GenBuyBaseActionsPost()

Rule "Buy Ultimate Ability"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gunlockUltimate[2], gunlockUltimate[0])
        pvar unlockedUltimate != true
    Actions
        GenBuyBaseActionsPre(gunlockUltimate[2], gunlockUltimate[0], gunlockUltimate[1])
        pvar unlockedUltimate = true
        Set Ultimate Ability Enabled
            Event Player
            True
        GenBuyBaseActionsPost()

Rule "Buy Self Kill"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gcommitSuicide[2], gcommitSuicide[0])
    Actions
        GenBuyBaseActionsPre(gcommitSuicide[2], gcommitSuicide[0], gcommitSuicide[1])
        Kill
            Event Player
            Null
        ExitLogic()
        GenBuyBaseActionsPost()

Rule "Buy Low Gravity"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gunlockLowGravity[2], gunlockLowGravity[0])
        pvar unlockedLowGravity != true
        pvar currentPrestige >= gunlockLowGravity[4]
    Actions
        GenBuyBaseActionsPre(gunlockLowGravity[2], gunlockLowGravity[0], gunlockLowGravity[1])
        pvar unlockedLowGravity = true
        pvar lowGravityEnabled = true
        Set Gravity
            Event Player
            50

Rule "Buy Aimbot"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gunlockAimbot[2], gunlockAimbot[0])
        pvar unlockedAimbot != true
        pvar currentPrestige >= gunlockAimbot[4]
    Actions
        GenBuyBaseActionsPre(gunlockAimbot[2], gunlockAimbot[0], gunlockAimbot[1])
        pvar unlockedAimbot = true
        BigHud(Left, `Banned Attack On: {}`(pvar aimbotEnabled), White)

Rule "Aimbot - Start"
    Event
        On Each Player
        All
        All
    Conditions
        pvar unlockedAimbot
        Is Button Held(Event Player, Primary Fire)
        Event Player.crouching
    Actions
        pvar aimbotEnabled = true

Rule "Aimbot - Stop"
    Event
        On Each Player
        All
        All
    Conditions
        pvar unlockedAimbot
        Is Button Held(Event Player, Secondary Fire)
        Event Player.crouching
    Actions
        pvar aimbotEnabled = false

Rule "Loop - Aimbot On Fire"
    Event
        On Each Player
        All
        All
    Conditions
        pvar unlockedAimbot
        pvar aimbotEnabled
        Is Button Held(Event Player, Primary Fire)
        Is Alive(Player Closest To Reticle(Event Player, All Teams))
    Actions
        Start Facing
            Event Player
            Direction Towards
                Event Player
                Player Closest To Reticle(Event Player, All Teams)
            100000
            To World
            Direction and Turn Rate

Rule "Loop - Aimbot On Not Firing"
    Event
        On Each Player
        All
        All
    Conditions
        pvar unlockedAimbot
        pvar aimbotEnabled != true
        not Is Button Held(Event Player, Primary Fire)
    Actions
        Stop Facing
            Event Player


Rule "Buy Multiplier"
    GenBuyEvent()
    Conditions
        Event Player.interacting == true
        Distance Between
            Event Player
            gvar buyMultiplierLocation
        <= 1.25
    Actions
        if pvar buyMultiplier == 1:
            pvar buyMultiplier = 10
        elif pvar buyMultiplier == 10:
            pvar buyMultiplier = 100
        elif pvar buyMultiplier == 100:
            pvar buyMultiplier = 1

        Msg(Event Player, `Buy Fast: {}`(pvar buyMultiplier))

/*
rule("Buy Multiplier") {
   Event {
      Ongoing - Each Player;
      All;
      All;
   }

   Conditions {
      Compare(Is Button Held(Event Player, Interact), ==, True) == True;
      Compare(Distance Between(Event Player, Value In Array(Global Variable(A), 3)), <=, 1.25) == True;
   }
   Actions {
      Skip If(Not(Compare(Value In Array(Player Variable(Event Player, A), 21), ==, 1)), 2);
      Set Player Variable At Index(Event Player, A, 21, 10);
      Skip(5);
      Skip If(Not(Compare(Value In Array(Player Variable(Event Player, A), 21), ==, 10)), 2);
      Set Player Variable At Index(Event Player, A, 21, 100);
      Skip(2);
      Skip If(Not(Compare(Value In Array(Player Variable(Event Player, A), 21), ==, 100)), 1);
      Set Player Variable At Index(Event Player, A, 21, 1);

      Small Message(Event Player, String("{0}: {1}", String("{0} {1}", String("Buy", Null, Null, Null), String("Fast", Null, Null, Null), Null), Value In Array(Player Variable(Event Player, A), 20), Null));
   }
}
 */

/*
Rule "Buy Base"
    GenBuyEvent()
    Conditions
        GenBuyConditions(gvar[2], gvar[0])
    Actions
        GenBuyBaseActionsPre(gvar[2], gvar[0], gvar[1])
        GenBuyBaseActionsPost()

 */

Disabled Rule "Hax"
    Event
        On Each Player
        All
        Slot 0
    Conditions
        Is Button Held(Event Player, Primary Fire)
        Is Button Held(Event Player, Ability 1)
        Is Button Held(Event Player, Ultimate)
    Actions
        pvar money += 100000

Rule "Debug Coordinates"
    Event
        On Each Player
        All
        All
    Conditions
        Event Player.crouching
        Event Player.interacting
        Is Firing Primary(Event Player)
    Actions
        Msg(Event Player, `You: {}`(Position Of(Event Player)))

Disabled Rule "Coordinate Viewer"
    Event
        On Each Player
        All
        All
    Conditions
        Has Spawned(Event Player)
        Event Player.moving
        Event Player.crouching
        Is Communicating Any Voice Line(Event Player)
    Actions
        Create Effect
            Visible_To: Event Player
            Type: Sphere
            Color: White
            Pos: Event Player.facing + Event Player.eyepos
            Radius: 0.20
            Reevaluation: Visible To
        Create Hud Text
            Visible_To: Event Player
            Header: Null
            Subheader: Null
            Text: Event Player.facing + Event Player.eyepos
            Location: Left
            Sort_Order: Null
            Header_Color: White
            Subheader_Color: White
            Text_Color: White
            Reevaluation: Visible To And String

%GenerateBuyRule(pos, color, price, name_)
    Rule "Buy Upgrade"
        Event
            On Each Player
            All
            All
        Conditions
            Event Player.interacting
            //pvar money >= price
            Distance Between(Event Player, pos) <= 1.25
        Actions
            //pvar money -= price
            Msg(Event Player, `{} {}`("Upgrade", "Bought"))
            Play Effect
                Visible_To: Event Player
                Type: ring explosion
                Color: color
                Position: pos
                Radius: 3